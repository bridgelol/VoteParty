import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.9.10'
	id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
	id "com.github.ben-manes.versions" version "0.47.0"
	id "net.kyori.indra" version "2.2.0"
	id "net.kyori.indra.publishing" version "2.2.0"
}

allprojects {
	apply plugin: 'org.jetbrains.kotlin.jvm'
	apply plugin: 'net.kyori.indra'
	apply plugin: 'net.kyori.indra.publishing'

	archivesBaseName = 'VoteParty'
	group 'me.clip'
	version '2.38-SNAPSHOT'

	repositories {
		mavenCentral()

		maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
		maven { url = "https://repo.aikar.co/content/groups/aikar/" }
		maven { url = "https://papermc.io/repo/repository/maven-public/" }
		maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
		maven { url = "https://repo.glaremasters.me/repository/public/" }
		maven { url = "https://repo.papermc.io/repository/maven-public/" }
	}

	dependencies {
		// kotlin
		implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

		// paper
		compileOnly("io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT")
	}

	java {
		toolchain.languageVersion.set(JavaLanguageVersion.of(17))
	}

	indra {
		mitLicense()

		javaVersions {
			target(17)
		}

		github("darbyjack", "voteparty") {
			publishing(true)
		}

		publishAllTo("nexus", "https://repo.glaremasters.me/repository/random-stuff/")
	}

	compileJava {
		options.compilerArgs += ["-parameters"]
	}

	compileKotlin {
		kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
		kotlinOptions.javaParameters = true
	}
}

apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
	minimize()
	relocate("co.aikar.commands", "me.clip.voteparty.libs.acf")
	relocate("co.aikar.locales", "me.clip.voteparty.libs.locales")
	relocate("ch.jalu.configme", "me.clip.voteparty.libs.configme")
	relocate("org.inventivetalent", "me.clip.voteparty.libs.inventivetalent")
	relocate("net.kyori", "me.clip.voteparty.libs.kyori")
	relocate("com.cryptomorin.xseries", "me.clip.voteparty.libs.xseries")
	relocate("kotlin", "me.clip.voteparty.libs.kotlin")
	relocate("org.bstats", "me.clip.voteparty.libs.bstats")
	archiveFileName = "VoteParty-${project.version}.jar"
}


dependencies {

	// config
	implementation "ch.jalu:configme:1.3.0"

	// placeholderapi
	compileOnly "me.clip:placeholderapi:2.11.5"

	// NuVotifier hook
	compileOnly files("libs/nuvotifier-2.7.3.jar")

	// command handler
	implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

	// Support for materials across all versions
	implementation 'com.github.cryptomorin:XSeries:9.8.1'

	// bstats
	implementation 'org.bstats:bstats-bukkit:3.0.2'

	// json stuff
	implementation 'net.kyori:adventure-platform-bukkit:4.3.2'
	implementation 'net.kyori:adventure-api:4.15.0'
	implementation 'net.kyori:adventure-text-minimessage:4.15.0'

	implementation project(":version")
	implementation project(":version_old")
	implementation project(":version_new")
}

processResources {
	filter ReplaceTokens, tokens: [version: version]
}
